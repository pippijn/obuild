#!/usr/bin/env perl

use common::sense;

use Cwd 'abs_path';
use File::Find;
use Text::CSV;
use Time::HiRes 'gettimeofday';

use constant {
   SRCDIR   => "src",
   TAGFILE  => (abs_path "src/.remake"),
   DEBUG    => 0,
};

my $DIRECT = $ARGV[0] ne "-on-demand";

my $exit = -f TAGFILE;

if (not $DIRECT) {
   shift @ARGV;
}

# Touch update-file.
open my $fh, '>', TAGFILE;
print $fh "Update requested at ", time;

exit if $exit;

my $omake = fork;
if ($omake) {
   $SIG{INT} = sub { exit };
   END { if ($omake) { kill 2, $omake; waitpid $omake, 0; unlink TAGFILE } }
} else {
   exec "omake", "-P", @ARGV
}

my $csv = new Text::CSV { binary => 1 };
open my $fh, '-|', qw|inotifywait -q -m --csv -e close_write -r|, SRCDIR;

my %links;
sub collect_links {
   my $start = gettimeofday;

   %links = ();
   find {
      wanted => sub {
         if (-l) {
            my $abs = abs_path $_ or die "$_: $!";
            push @{ $links{$abs} }, $_;
         }
      },
      no_chdir => 1,
   }, '.';

   my $end = gettimeofday;

   my $sum = 0;
   $sum += @$_ for values %links;
   printf "*** remake: watching %d symbolic links to %d files (%.02f sec)\n"
      , $sum
      , scalar keys %links
      , ($end - $start)
      ;
}

sub update_link {
   my ($target) = @_;
   my $source = readlink $target;

   if (DEBUG) {
      printf "*** remake: updating link %s -> %s\n"
         , $target
         , $source
         ;
   }
   # This is an atomic link-touch, so inotify in omake is triggered.
   #system "ln", "-sf", $source, $target;
   kill 19, $omake; # STOP omake
   unlink $target; # remove old link
   symlink $source, $target or die $!; # re-instate link
   kill 18, $omake; # CONT omake
}

# Initial reading of symlinks.
collect_links;

# Hash-set of changed nodes.
my %changed;
while (my $row = $csv->getline ($fh)) {
   my @events = split ',', $row->[1];
   my $file = abs_path "$row->[0]$row->[2]";

   if ($file eq TAGFILE) {
      update_link $_ for keys %changed;
      %changed = ();

      # This is actually too early, so maybe the user needs to
      # call it again after omake has finished.
      collect_links;
   } else {
      for my $target (@{ $links{$file} }) {
         if (not $DIRECT) {
            $changed{$target} = undef;
         } else {
            update_link $target
         }
      }
   }
}
