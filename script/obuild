#!/usr/bin/env perl

use common::sense;

use Cwd 'abs_path', 'getcwd';
use Dir::Self;
use File::Basename;


# obuild's installation path.
my $rootdir = abs_path (dirname __DIR__);
my $curdir = getcwd;


my $GENERATED = "Generated by obuild";

sub if_generated (&$) {
   if (open my $fh, '<', $_[1]) {
      my $line = <$fh>;
      $_[0]->($_ = $_[1])
         if $line =~ /$GENERATED/;
   } elsif (not -f $_[1]) {
      $_[0]->($_ = $_[1])
   }
}

sub getbuilddir {
   my $cwd = getcwd;
   while (not -d '_build') {
      my $cwd = getcwd;
      chdir "..";
      chdir $curdir and last
         if getcwd eq $cwd;
   }

   my $projdir = getcwd;
   my ($reldir) = $curdir =~ m|$projdir(?:/_build)?/?(.*)|;

   # chdir back to where we came from
   chdir $cwd;

   # Insert "_build" between $projdir and the relative builddir.
   if ($reldir) {
      ($projdir, "$projdir/_build/$reldir", $reldir)
   } else {
      ($projdir, $projdir, '.')
   }
}

# Find the current project directory and change to it,
# putting the relative subdir into $builddir.
my ($projdir, $builddir, $reldir) = getbuilddir;

# Just print installation path.
if ($ARGV[0] eq '--where') {
   print $rootdir;
   exit;
}

# Print project root path.
if ($ARGV[0] eq '--project') {
   print $projdir;
   exit;
}

# Directories we will produce during build.
my @builddirs = (
   "_build",
   "_install",
   "_tests",
);

# Remove build directories and generated files.
if ($ARGV[0] eq '--clean') {
   print "*** obuild: cleaning up\n";
   system "rm", "-rf", @builddirs;
   system "rm", "-f", ".times", glob ".*omakedb*";
   system "find", ".", "-name", "*.omc", "-delete";

   if_generated { unlink $_ } 'OMakeroot';
   if_generated { unlink $_ } '.cvsignore';

   exit;
}

my $configure = grep { $_ eq '--configure' } @ARGV;
if ($projdir eq abs_path $reldir) {
   if (not -d "_build" and not $configure) {
      die "*** obuild: error: project not configured, run 'obuild --configure'\n";
   }
}

if ($configure) {
   mkdir for map { "$projdir/$_" } @builddirs;

   # Generate a default OMakeroot and .cvsignore if no user supplied ones exist.
   if_generated {
      open my $fh, '>', $_[0]
         or die "Could not create $_[0]: $!";
      say $fh "# $GENERATED";
      say $fh "open rules/default";
   } 'OMakeroot';
   if_generated {
      my %ignore = map { $_ => undef } do {
         my @ignore = @builddirs;
         if (open my $gitignore, '<', '.gitignore') {
            chomp  and # kill newline
            s|^/|| and # kill leading slash
            push @ignore, $_
               for <$gitignore>
         }
         @ignore
      };

      open my $fh, '>', $_[0]
         or die "Could not create $_[0]: $!";
      say $fh "# $GENERATED";
      say $fh $_ for sort keys %ignore;
   } '.cvsignore';
}

# Preserve OMake stdlib location.
my ($STDLIB) = `omake --version` =~ /Default library directory : (.+)/;

# Add obuild path to OMake's include path.
$ENV{OMAKEPATH} = "$STDLIB:$rootdir";

# Change to relative build directory.
my $targets = do {
   if (my @targets = grep { !/^-/ } @ARGV) {
      join ", ", @targets
   } else {
      ".DEFAULT"
   }
};
if ($ARGV[0] eq '--no-chdir') {
   shift @ARGV;
} else {
   print "*** obuild: building $targets in $reldir ($builddir)\n";
   chdir $builddir
      or die "could not chdir to '$builddir': $!";
}

my @omake = ("omake", "rootdir=$rootdir");

if ($ARGV[0] eq '--valgrind') {
   unshift @omake, "valgrind", "--tool=callgrind";
}

# Kick off build with user-supplied arguments.
exec @omake, @ARGV;
die "Failed to execute '@omake': $!";
